#program nexions
#autoline 10,10
#; 
#; Global Variables
#; 
#; %a - Fire button count, prevent user holding down fire
#; %b
#; %c - Enemy count
#; %d
#; %e
#; %f
#; %g
#; %h - tmp
#; %i - tmp
#; %j - tmp
#; %k - tmp
#; %l - Layer Y position, used to scroll starfield
#; %m - Current missile sprite ID, 7,8,9
#; %n 
#; %o 
#; %p 
#; %q
#; %r
#; %s - TODO Current State
#; %t
#; %u
#; %v
#; %w
#; %x - Ship X position
#; %y - Ship Y position
#; %z
#; lives - Current number of lives 
#; score - Current score
#; high - High score
#; timer - Last value of TIME, used to measure time duration (1/50th second) 
#; 
#; 
#; 
#; Game States:
#; 0 title 
#; 1 start 
#; 2 newWave
#; 3 game
#; 4 die
#; 5 gameOver
#; 
#; 
#; 
#; 
#; 

RUN AT 3
REM MPD Bailey 4 Jan 2024
REM Nexions
#; Constants for states
sTitle, sStart, sWave, sGame, sDie, sGameOver=0,1,2,3,4,5 
PROC state(sTitle)

#; 
#; Main game loop
#; 
@gameLoop: 
ON %s: proc title(): PROC start(): PROC wave(): PROC game(): PROC die(): PROC gameOver()
GO TO @gameLoop

#;
#; Enter new state
#;
DEFPROC state(newState)
%s = newState
ON %s: proc sTitle(): PROC sStart(): PROC sWave(): PROC sGame(): PROC sDie(): PROC sGameOver()
ENDPROC

DEFPROC sTitle()
PROC initGraphics()
LOAD "title.sl2" LAYER 
ENDPROC

#; Title screen
#; Press fire to start game
#;
DEFPROC title()
%i = INPUT 1| INPUT 0
IF %i&$10 THEN PROC state(sStart)
ENDPROC

#;
#; Initialize for a new game
#;
DEFPROC sStart()
PROC initGraphics()
%x,%y,%l,%m,%a=%152,%210,%0,%7,%0
lives=3
score=0
LOAD "stars.sl2" LAYER 
ENDPROC

DEFPROC start()
PROC update()
PROC display()
PROC score() : PROC lives()
#; Change state to new wave
PROC state(sWave)
ENDPROC

#;
#; init state: new wave of enemies
#;
DEFPROC sWave()
%c=5
FOR %i=%0 TO %4
SPRITE %10+i,%30 + RND 200,%30 + RND 100,%5,%1
SPRITE CONTINUE %10+i,%30 TO %230 STEP %1 RUN ,%30 TO %150 STEP %2 RUN ,%7,%0
NEXT %i
ENDPROC

DEFPROC Wave()
#; Change state to play
PROC state(sGame)
ENDPROC

DEFPROC sGame()
ENDPROC

DEFPROC game()
PROC handleInput()
PROC update()
PROC display()
PROC bomb()
ENDPROC

DEFPROC sDie()
#; Lose a life
timer = TIME
lives-=1
PROC lives()
ENDPROC

DEFPROC die()
PROC display()

#; Wait for 2 seconds
BEEP 0.05,RND(50)-60
IF (TIME-timer)< 100 THEN ENDPROC 
#; Reset the ship positions
%x=%152

#;Reset bullet count and Fire press
%m, %a = %7,%0

#; Back to the game
IF lives=0 THEN PROC state(sGameOver) : ELSE PROC state(sGame)
ENDPROC

DEFPROC sGameOver()
timer = TIME
LAYER %0: PRINT AT %10,%10;"Game Over"
LAYER %2,%1
ENDPROC

DEFPROC gameOver()
PROC display()
#; Wait for 2 seconds
IF (TIME-timer)< 100 THEN ENDPROC 
%i = INPUT 1| INPUT 0
IF %i&$10 THEN PROC state(sTitle)
ENDPROC

#; 
#; Set up Layers and Sprites
#; 
DEFPROC initGraphics()

#; Layers
LAYER 0: BORDER 0: PAPER 0: INK 7: CLS 
LAYER CLEAR
LAYER OVER 6
LAYER 2,1: LAYER ERASE 0,0,256,192

#; Sprites
BANK NEW bankId
LOAD "sprites.spr" BANK bankId
SPRITE CLEAR 
SPRITE BANK bankId
BANK bankId CLEAR
SPRITE PRINT 1
SPRITE STOP 

ENDPROC 

#; 
#; 
#; 
DEFPROC handleInput()
LOCAL %p = INPUT 1| INPUT 0
IF %p&1 THEN %x+=2
IF %p&2 THEN %x-=2
#; Prevent user holding down the fire button
IF %p&$10 THEN %a+=1 : ELSE %a=0
IF %a=1 THEN PROC fire()
IF %x<32 THEN %x=%32
IF %x>271 THEN %x=%272
ENDPROC 

#; 
#;Scroll star field downwards and move sprites 
#; 
DEFPROC display()
%l-=1
IF %l=-1 THEN %l=191
SPRITE %0,%x,%y,%0,%@1101
SPRITE MOVE INT 
LAYER AT %0,%l
ENDPROC 

#; 
#; 
#; 
DEFPROC update()
FOR %i=%7 TO %9
%h = SPRITE OVER (%i,%10 TO %20,%7,%7) 
IF %h>0 THEN PROC destroyEnemy(%h,%i)
NEXT %i
#; If hit by a enemy missile, enter state die
IF SPRITE OVER (%0,%20 TO %24,%7,%2) THEN PROC state(sDie) 
ENDPROC 

DEFPROC score()
LAYER %0: PRINT AT %0,%0;score
LAYER %2,%1
ENDPROC 

DEFPROC lives()
LAYER %0: PRINT AT %0,%31;lives
LAYER %2,%1
ENDPROC 


#; 
#; 
#; 
DEFPROC fire()
#; Exit if 3 missiles flying
IF SPRITE 9 THEN ENDPROC
BEEP .1,0
SPRITE %m,%x,%200,%4,1
SPRITE CONTINUE %m,%x,%0 TO %200 STEP -8 RUN,%4,%3
%m+=%1: IF %m>9 THEN %m=%7
ENDPROC 

#; 
#; 
#; 
DEFPROC destroyEnemy(%s,%b)
SPRITE %s,%0,%0,%0,%0: SPRITE %b,%0,%0,%0,%0
%c-=%1
BEEP .1,-5
#; No Enemies left, change state to newWave
IF %c=0 THEN PROC state(sWave)
#; Update the score
score+=50
PROC score()
ENDPROC 

#; 
#; 
#; 
DEFPROC bomb()
PRIVATE bomb=0
LOCAL %x,%y 
sId = bomb+10:bId=20+bomb
IF NOT SPRITE sId OR SPRITE bId THEN GO TO @noBomb
IF % RND 20<>5 THEN GO TO @noBomb
%x = SPRITE AT (sId,0)
%y = SPRITE AT (sId,1)
SPRITE bId,%x,%y,%5,1
SPRITE CONTINUE bId,,%x TO 200 STEP 2 RUN ,%5,3
@noBomb: bomb+=1: IF bomb=5 THEN bomb =0
ENDPROC 

#autoline