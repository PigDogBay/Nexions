#program nexions
#autoline 10,10
#; 
#; GLOBAL VARIABLES
#; ----------------
#; 
#; %a - Fire button count, prevent user holding down fire
#; %b - Pattern change counter
#; %c - Enemy count
#; %d - Enemy sprite pattern
#; %e - Bomb count
#; %f
#; %g - tmp
#; %h - tmp
#; %i - tmp
#; %j - tmp
#; %k - tmp
#; %l - Layer Y position, used to scroll starfield
#; %m - Current missile sprite ID, 7,8,9
#; %n 
#; %o 
#; %p 
#; %q
#; %r
#; %s - Current State
#; %t
#; %u
#; %v - Sub wave number of enemies: 5,7,9
#; %w - Wave 1 - 20
#; %x - Ship X position
#; %y - Ship Y position
#; %z
#; lives - Current number of lives 
#; score - Current score
#; high - High score
#; timer - Last value of TIME, used to measure time duration (1/50th second) 
#; 
#; 
#; 
#; GAME STATES
#; -----------
#;
#; 0 sTitle 
#; 1 sStart 
#; 2 sWave
#; 3 sNewWave
#; 4 sSubWave
#; 5 sGame
#; 6 sDie
#; 7 sGameOver
#; 
#; 
#; 
#; 
#; 
#; SPRITES
#; -------
#; 
#; 0 - Player ship
#; 7,8,9 - Player bullets
#; 10-18 - Enemies
#; 19 - Mothership
#; 20-28 - Enemy Bombs
#; 29 - Bouncing bomb
#; 50-53 - Enemy explosions
#; 
#; 

RUN AT 3
REM MPD Bailey 4 Jan 2024
REM Nexions

PROC initSnd()

#; Constants for states
sTitle, sStart, sWave, sNewWave, sSubWave, sGame, sDie, sGameOver=0,1,2,3,4,5,6,7
PROC state(sTitle)
PROC readData()


#;-----------------------------
#;
#; State Machine Functions
#;
#;-----------------------------

@g 
ON %s: proc title(): PROC start(): PROC wave(): PROC newWave(): PROC subWave(): PROC game(): PROC die(): PROC gameOver()
PROC anim()
GO TO @g


#;
#; Enter new state and initialize it
#;
DEFPROC state(newState)
%s = newState
ON %s: proc sTitle(): PROC sStart(): PROC sWave(): PROC sNewWave(): PROC sSubWave(): PROC sGame(): PROC sDie(): PROC sGameOver()
ENDPROC

#; 
#; *** ENTER STATE: TITLE  ***
#; 
DEFPROC sTitle()
PROC initGraphics()
LOAD "title.sl2" LAYER
DRIVER %49,%2,%2
ENDPROC
#; 
#; *** STATE: TITLE ***
#; 
DEFPROC title()
%i = INPUT 1| INPUT 0
#; Fire to start game
IF %i&$10 THEN PROC state(sStart)
ENDPROC


#; 
#; *** STATE: START ***
#; 
DEFPROC sStart()
PROC initGraphics()
%x,%y,%l,%m,%a=%152,%208,%0,%7,%0
%w,%v=1,5
lives=3
score=0
LOAD "stars.sl2" LAYER
ENDPROC
#; 
#; *** ENTER STATE: START***
#; 
DEFPROC start()
PROC display()
PROC score() : PROC lives()

#; Enable player ship and animate between patterns 8-9
SPRITE %0,%x,%y,%9,%1
SPRITE CONTINUE %0,,,%8 TO %9,$4,%3,%0

#; Change state to new wave
PROC state(sWave)
ENDPROC

#; 
#; *** ENTER STATE: WAVE ***
#; 
DEFPROC sWave()
timer = TIME
IF %v=5 THEN PROC state(sNewWave): ELSE PROC state(sSubWave)
ENDPROC
#; 
#; *** ENTER STATE: WAVE ***
#; 
DEFPROC Wave()
ENDPROC


#; 
#; *** ENTER STATE: NEW WAVE***
#; 
DEFPROC sNewWave() 
DRIVER %49,%2,%5
PROC display()
LAYER %0: PRINT AT %10,%10;title$(%w)
LAYER %2,%1
ENDPROC
#; 
#; *** ENTER STATE: NEW WAVE ***
#; 
DEFPROC newWave() 
PROC display()
IF (TIME-timer)<50 THEN ENDPROC
LAYER %0: PRINT AT %10,%10;"                 "
LAYER %2,%1
PROC state(sSubWave)
ENDPROC


#; 
#; *** ENTER STATE: SUBWAVE ***
#; 
DEFPROC sSubWave() 
PROC display()
ENDPROC
#; 
#; *** ENTER STATE: SUBWAVE ***
#; 
DEFPROC subWave() 
PROC display()
%c=%v
%d=%p(w)
FOR %i=%1 TO %v
SPRITE %9+i,%30 + RND 200,%30 + RND 120,%d,%1
SPRITE CONTINUE %9+i,%30 TO %230 STEP %1 RUN ,%30 TO %150 STEP %2 RUN,%d,%0
NEXT %i
#; Mother ship appears after random delay
SPRITE %19,%16,%32,%34,%1
SPRITE CONTINUE %19,%16 TO %304 STEP %2 RUN,,%34,%11,,%20+RND(100)
%v+=2
IF %v > 9 THEN %w+=1:%v=5
PROC state(sGame)  
ENDPROC

#; 
#; *** ENTER STATE: GAME ***
#; 
DEFPROC sGame():ENDPROC
#; 
#; *** ENTER STATE: GAME ***
#; 
DEFPROC game()
PROC display()
PROC handleInput()
PROC bomb()
PROC colDet() TO %i
IF %i THEN PROC state(sDie)
#; No Enemies left, change state to sWave
IF %c<1 THEN PROC state(sWave)
ENDPROC

#; 
#; *** ENTER STATE: DIE ***
#; 
DEFPROC sDie()
#; Lose a life
timer = TIME
lives-=1
PROC lives()

#;Show ship explosion patterns
#;Patterns 48 to 53
#;Flags Disable when reach limit and update when stationary
#;Rate every 10 calls, delay 5 calls
SPRITE %0,%x,%y,%48,%1
SPRITE CONTINUE %0,,,%48 TO %53,$60,%5,%5
DRIVER %49,%2,%22
PROC clearBombs()
ENDPROC


#; 
#; *** STATE: DIE ***
#; 
DEFPROC die()
PROC display()

#; Wait for 2 seconds
IF (TIME-timer)< 100 THEN ENDPROC 
#; Reset the ship positions
%x=%152

#;Reset bullet count and Fire press
%m, %a = %7,%0

#; Back to the game
IF lives=0 THEN PROC state(sGameOver) : ENDPROC
#;New ship
SPRITE %0,%x,%y,%9,%1
SPRITE CONTINUE %0,,,%8 TO %9,$4,%3,%0

PROC state(sGame)
ENDPROC


#; 
#; *** ENTER STATE: GAME OVER  ***
#; 
DEFPROC sGameOver()
timer = TIME
LAYER %0: PRINT AT %10,%10;"Game Over"
LAYER %2,%1
ENDPROC
#; 
#; *** STATE: GAME OVER  ***
#; 
DEFPROC gameOver()
PROC display()
#; Wait for 2 seconds
IF (TIME-timer)< 100 THEN ENDPROC 
%i = INPUT 1| INPUT 0
IF %i&$10 THEN PROC state(sTitle)
ENDPROC


#;-----------------------------
#;
#; Utility Functions
#; Do NOT change state in these
#;
#;-----------------------------


#; 
#; *** INITIALIZE GRAPHICS ***
#; 
#; Set up Layers and Sprites
DEFPROC initGraphics()

#; Layers
LAYER 0: BORDER 0: PAPER 0: INK 7: CLS 
LAYER CLEAR
LAYER OVER 6
LAYER 2,1: LAYER ERASE 0,0,256,192

#; Sprites
BANK NEW bankId
LOAD "sprites.spr" BANK bankId
SPRITE CLEAR 
SPRITE BANK bankId
BANK bankId CLEAR
SPRITE PRINT 1
SPRITE STOP 

ENDPROC 

#; Set up AY sound driver and load sound effects
#; If driver is already installed, will throw an error
#; So this code will catch and ignore the error
DEFPROC initSnd()
    ON ERROR ENDPROC  
    .install "ayfx.drv"
    BANK NEW %i
    LOAD "sfx.afb" BANK %i
    DRIVER 49,1,%i
ENDPROC

#; 
#; *** HANDLE INPUT ***
#; 
DEFPROC handleInput()
%i = INPUT 1| INPUT 0
IF %i&1 THEN %x+=3
IF %i&2 THEN %x-=3
#; Prevent user holding down the fire button
IF %i&$10 THEN %a+=1 : ELSE %a=0
IF %a=1 THEN PROC fire()
IF %x<32 THEN %x=%32
IF %x>271 THEN %x=%272
ENDPROC 


#; 
#; *** UPDATE DISPLAY  ***
#; 
#;Scroll star field downwards and move sprites 
DEFPROC display()
%l-=1
IF %l=-1 THEN %l=191

#! Update player sprite position and pattern
SPRITE %0,%x,%y

SPRITE MOVE INT 
LAYER AT %0,%l
ENDPROC 


#; 
#; *** COLLISION DETECTION ***
#; 
DEFPROC colDet()
FOR %i=%7 TO %9
%h = SPRITE OVER (%i,%10 TO %19,%7,%7) 
IF %h>0 THEN PROC destroyEnemy(%h,%i)
NEXT %i

#; Return true if player has been hit
ENDPROC = SPRITE OVER (%0,%20 TO %29,%7,%2)

#; 
#; *** UPDATE SCORE ***
#; 
DEFPROC score()
LAYER %0: PRINT AT %0,%0;score
LAYER %2,%1
ENDPROC 



#; 
#; *** ANIMATE ENEMY SPRITES ***
#;
#; Cannot use SPRITE CONTINUE to automate the pattern change
#; as it is been updated to quickly, (set a rate will slow movement)
#; So need manually change the pattern every 10 calls
#;
DEFPROC anim() 
%b+=1: IF (%b<>10) AND (%b<>20) THEN ENDPROC
%j=%d
SPRITE CONTINUE %19,,,%34
IF %b=20 THEN %j+=1 :%b=0:SPRITE CONTINUE %19,,,%35
FOR %i=%10 TO %18
SPRITE CONTINUE %i,,,%j
NEXT %i
ENDPROC

#; 
#; *** UPDATE LIVES ***
#; 
DEFPROC lives()
LAYER %0: PRINT AT %0,%31;lives
LAYER %2,%1
ENDPROC

#; 
#; *** PLAYER FIRE ***
#; 
DEFPROC fire()
#; Exit if 3 missiles flying
IF SPRITE 9 THEN ENDPROC
DRIVER %49,%2,%11
SPRITE %m,%x,%200,%36,%1
SPRITE CONTINUE %m,%x,%0 TO %200 STEP -8 RUN,%36 TO %37,%3
%m+=%1: IF %m>9 THEN %m=%7
ENDPROC 


#; 
#; *** DESTROY ENEMY ***
#; 
DEFPROC destroyEnemy(%s,%b)
#; Get enemy x,y pos
%i=SPRITE AT (%s,0)
%j= SPRITE AT (%s,1)
PROC bang(%i,%j)
SPRITE %s,%0,%0,%0,%0: SPRITE %b,%0,%0,%0,%0
IF %s=19 THEN score+=450:DRIVER %49,%2,%1:ELSE %c-=%1
DRIVER %49,%2,%21
#; Update the score
score+=50
PROC score()
ENDPROC


#; 
#; *** CLEAR ENEMIES ***
#; 
DEFPROC clearEnemies()
FOR %i=%10 TO %19
SPRITE %i,%0,%0,%0,%0: SPRITE %i+10,%0,%0,%0,%0
NEXT %i
ENDPROC

DEFPROC clearBombs()
FOR %i=%20 TO %29
SPRITE %i,%0,%0,%0,%0
NEXT %i
ENDPROC

#; 
#; *** ENEMY BOMB ***
#; 
DEFPROC bomb()
sId = %e+10:bId=%e+20:%e+=1: IF %e=10 THEN %e=%0
IF NOT SPRITE sId OR SPRITE bId THEN ENDPROC
%i= SPRITE AT (sId,%0)
#;More chance of dropping bomb closer to enemy
IF %x>i THEN %k=%x-i:ELSE %k=%i-x
IF %w < RND (k+75) THEN ENDPROC
%j = SPRITE AT (sId,%1)
SPRITE bId,%i,%j,%5,%1
%k=%w>>2 + 4
SPRITE CONTINUE bId,,%0 TO 225 STEP %k RUN ,%5,%3
ENDPROC 
#;IF sId=19 AND %i > 200
#;Drop a bouncing bomb
#;ENDIF

#;
#; Defender style explosion for enemies
#;
DEFPROC bang(%x,%y)
SPRITE 50,%x,%y,43,1
SPRITE 51,%x,%y,43,1
SPRITE 52,%x,%y,43,1
SPRITE 53,%x,%y,43,1
%j=%1+ RND 10
%k=%10-j
SPRITE CONTINUE 50,0 TO %x  STEP %SGN{-j} RUN, 0 TO %y STEP %SGN{-k} RUN, 43,3
%j=%1+ RND 10
%k=%10 - j
SPRITE CONTINUE 51,%x TO 256 STEP %j RUN, %y TO 192 STEP %k RUN, 43,3
%j=%1+ RND 10
%k=%10 - j
SPRITE CONTINUE 52,%x TO 256 STEP %j RUN, 0 TO %y STEP %SGN{-k} RUN, 43,3
%j=%1+ RND 10
%k=%10 - j
SPRITE CONTINUE 53,0 TO %x STEP % SGN {-j} RUN ,%y TO 192 STEP %k RUN ,43,3
ENDPROC

#; 
#; *** DEBUG PRINT ***
#; 
DEFPROC dbg(%i)
LAYER %0: PRINT AT %21,%0;%i;"    "
LAYER %2,%1
ENDPROC


DEFPROC readData()
DIM title$(20,18)
RESTORE @Wave
FOR %i=1 TO 20
READ title$(%i),%p(i)
NEXT %i
ENDPROC

#;-----------------------------
#;
#; WAVE DATA
#;
#;-----------------------------
@Wave
DATA "1. The Invasion",%12
DATA "2. Flagship",%16
DATA "3. Whoovians",%14
DATA "4. Coa Monster",%26
DATA "5. Brain Wave",%22
DATA "6. Sausage Time",%54
DATA "7. Nemesis",%10
DATA "8. Golden Age",%32
DATA "9. Full Spectrum",%56
DATA "10. Brain Drain",%22
DATA "11. Little Acorns",%58
DATA "12. RTM",%0
DATA "13. Squid Game",%6
DATA "14. !#$! Q",%18
DATA "15. Brain Freeze",%22
DATA "16. Scorpio",%30
DATA "17. Spectres",%20
DATA "18. Doh",%60
DATA "19. Space Lair",%24
DATA "20. Doomed",%28

#autoline