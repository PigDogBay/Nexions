#program nexions
#autoline 10,10
#; MPD Bailey Technology 4 Jan 2024
#; Nexions
#; 
#; GLOBAL VARIABLES
#; ----------------
#; 
#; %a - Fire button count, prevent user holding down fire
#; %b - Pattern change counter
#; %c - Enemy count
#; %d - Enemy sprite pattern
#; %e - Bomb count
#; %f - Spider bomb counter
#; %g - tmp
#; %h - tmp
#; %i - tmp
#; %j - tmp
#; %k - tmp
#; %l - Layer Y position, used to scroll starfield
#; %m - Current missile sprite ID, 7,8,9
#; %n
#; %o 
#; %p 
#; %q
#; %r
#; %s - Current State
#; %t - Title state
#; %u
#; %v - Sub wave number of enemies: 5,7,9
#; %w - Wave 1 - 20
#; %x - Ship X position
#; %y - Ship Y position
#; %z - Star sprite ID
#; lives - Current number of lives 
#; score - Current score
#; high - High score
#; Bonus - Shots fired, death will reduce bonus
#; timer - Last value of TIME, used to measure time duration (1/50th second) 
#; tmr2 - Another timer
#; Arrays
#; titles$ - Level names
#; a%[] - Enemy pattern
#; b%[] - Bomb speed
#;
#;
#; GAME STATES
#; -----------
#;
#; 0 sTitle 
#; 1 sStart 
#; 2 sWave
#; 3 sNewWave
#; 4 sSubWave
#; 5 sGame
#; 6 sDie
#; 7 sGameOver
#; 
#; 
#; 
#; 
#; 
#; SPRITES
#; -------
#; 
#; 0 - Player ship
#; 7,8,9 - Player bullets
#; 10 - Mothership
#; 11-19 - Enemies
#; 20 - Spider bomb
#; 21-29 - Enemy Bombs
#; 50-53 - Enemy explosions
#; 
#; 

RUN AT 3
#; Constants for states
sTitle, sStart, sWave, sNewWave, sSubWave, sGame, sDie, sInvuln, sGameOver=0,1,2,3,4,5,6,7,8
LOAD "data.bin" INT: LOAD "titles.arr" DATA title$():PROC initSnd():proc font(): PROC loadHS() TO high :PROC state(sTitle)

#;-----------------------------
#;
#; State Machine Functions
#;
#;-----------------------------

@g 
ON %s: proc title(): PROC start(): PROC wave(): PROC newWave(): PROC subWave(): PROC game(): PROC die(): PROC invuln():PROC gameOver()
PROC anim():GO TO @g


#;
#; Enter new state and initialize it
#;
DEFPROC state(newState)
%s = newState
ON %s: proc sTitle(): PROC sStart(): PROC sWave(): PROC sNewWave(): PROC sSubWave(): PROC sGame(): PROC sDie(): PROC sInvuln():PROC sGameOver()
ENDPROC

#; 
#; *** ENTER STATE: TITLE  ***
#; 
DEFPROC sTitle()
PROC initGraphics()
LOAD "title.sl2" LAYER
DRIVER %49,%2,%2
SPRITE %60,%140,%150,%6,%1
SPRITE CONTINUE %60,,,%6 TO %7,$40,%10
timer= TIME :tmr2= TIME
%z=%80
ENDPROC
#; 
#; *** STATE: TITLE ***
#; 
DEFPROC title()
PROC star()
IF (TIME-tmr2)>100 THEN PROC help():tmr2=TIME
SPRITE MOVE INT
#; Fire to start game
%i = INPUT 1| INPUT 0
IF %i&$10 THEN PROC state(sStart)
ENDPROC

DEFPROC help()
LAYER 0
PRINT AT 20,4;"                      "
IF %t=0 : PRINT AT 20,9;INK 4;"Enemy 50 pts":SPRITE CONTINUE %60,,,%6 TO %7:SPRITE %60,,,%6
ELSE IF %t=1 : PRINT AT 20,6;INK 6;"Mothership 250 pts":SPRITE CONTINUE %60,,,%46 TO %47:SPRITE %60,,,%46
ELSE IF %t=2 : PRINT AT 20,6;INK 5;"Spider Bomb 500 pts":SPRITE CONTINUE %60,,,%38 TO %39:SPRITE %60,,,%38
ELSE IF %t=3 : PRINT AT 20,9;INK 2;"Fire To Start":SPRITE CONTINUE %60,,,%22 TO %23:SPRITE %60,,,%22
ELSE IF %t=4 : PRINT AT 20,8;INK 7;"Hi Score ";high
ELSE: PRINT AT 18,8;INK 3;"By Pig Dog Bay"
ENDIF
%t+=1:IF %t=5 THEN %t=0
ENDPROC



#; 
#; *** STATE: START ***
#; 
DEFPROC sStart()
PROC initGraphics()
%x,%y,%l,%m,%a=%152,%208,%0,%7,%0
%w,%v=0,5
lives,score,bonus=3,0,0
LOAD "stars.sl2" LAYER
ENDPROC
#; 
#; *** ENTER STATE: START***
#; 
DEFPROC start()
PROC display()
PROC score() :PROC hiScore(): PROC lives()
#; Enable player ship and animate between patterns 8-9
SPRITE %0,%x,%y,%9,%1
SPRITE CONTINUE %0,,,%8 TO %9,%$50,%3,%0
#; Change state to new wave
PROC state(sWave)
ENDPROC

#; 
#; *** ENTER STATE: WAVE ***
#; 
DEFPROC sWave(): PROC clearEnemies(): SPRITE %0,%x,%y,%9,%1:ENDPROC
#; 
#; *** ENTER STATE: WAVE ***
#; 
DEFPROC Wave()
PROC handleInput()
PROC display()
PROC colDet()
#; Check if mothership from last wave has cleared
IF % SPRITE 10 THEN ENDPROC
timer = TIME
IF %v=5 THEN PROC state(sNewWave): ELSE PROC state(sSubWave)
ENDPROC

#; 
#; *** ENTER STATE: NEW WAVE***
#; 
DEFPROC sNewWave()
%w+=1
IF %w=31 THEN %w=1
DRIVER %49,%2,%5
#; Centre text
%i=(29- LEN title$(%w)[>])/2
LAYER %0: PRINT AT %10,%i;%w;". ";title$(%w)
IF bonus<0 THEN bonus = 50
IF %w > 1 THEN PRINT AT %0,%6; INK 6;"+";bonus
score+=bonus:bonus=1000
LAYER %2,%1
ENDPROC
#; 
#; *** ENTER STATE: NEW WAVE ***
#; 
DEFPROC newWave() 
PROC handleInput()
PROC display()
IF (TIME-timer)<100 THEN ENDPROC
LAYER %0: PRINT AT %10,%4;"                            ":PRINT AT %0,%6;"       "
LAYER %2,%1
PROC score()
PROC state(sSubWave)
ENDPROC


#; 
#; *** ENTER STATE: SUBWAVE ***
#; 
DEFPROC sSubWave()
timer = TIME:%c=%v:%d=%a(w)
ENDPROC
#; 
#; *** ENTER STATE: SUBWAVE ***
#; 
DEFPROC subWave() 
PROC handleInput()
PROC display()
IF ( TIME -timer) < 25 THEN ENDPROC

#;Set up enemy formation
IF %c(w)=0 THEN PROC rndFormation(): ELSE PROC formation(%c(w))

#; Mother ship appears after random delay
SPRITE %10,%16,%32,%45,%1
SPRITE CONTINUE %10,%0 TO %319 STEP %2 RUN ,,%45,%11,,%20+ RND (100)
%v+=2: IF %v>9 THEN %v=%5
PROC state(sGame)  
ENDPROC

#; 
#; *** ENTER STATE: GAME ***
#; 
DEFPROC sGame()
PROC display():PROC clearBombs()
ENDPROC
#; 
#; *** ENTER STATE: GAME ***
#; 
DEFPROC game()
PROC display()
PROC handleInput()
PROC bomb()
PROC colDet() TO %i
IF %i THEN PROC state(sDie)
#; No Enemies left, change state to sWave
IF %c < 1 THEN PROC state(sWave)
%n-=10
ENDPROC

#; 
#; *** ENTER STATE: Invulnerability ***
#; Clear any bombs and spider bomb
DEFPROC sInvuln()
PROC clearBombs(): SPRITE %20,%0,%0,%0,%0:timer= TIME
#;New ship
SPRITE %0,%x,%y,%9,%1
SPRITE CONTINUE %0,,,%8 TO %9,%$50,%3,%0
PROC lives()
ENDPROC
#; 
#; *** ENTER STATE: Invulnerability ***
#; No bombs
#; 
DEFPROC invuln()
PROC display()
PROC handleInput()
PROC colDet()
IF (TIME-timer)>75 THEN PROC state(sGame)
ENDPROC

#; 
#; *** ENTER STATE: DIE ***
#; 
DEFPROC sDie()
#; Lose a life
timer = TIME
lives-=1

#;Show ship explosion patterns
#;Patterns 48 to 53
#;Flags Disable when reach limit and update when stationary
#;Rate every 10 calls, delay 5 calls
SPRITE %0,%x,%y,%48,%1
SPRITE CONTINUE %0,,,%48 TO %53,$60,%5,%5
DRIVER %49,%2,%22
PROC clearBombs()
#; Reduce bonus
bonus-=200
ENDPROC


#; 
#; *** STATE: DIE ***
#; 
DEFPROC die()
PROC display()

#; Wait for 2 seconds
IF (TIME-timer)< 100 THEN ENDPROC 
#; Reset the ship positions
%x=%152

#;Reset bullet count and Fire press
%m, %a = %7,%0

#; Back to the game
IF lives=0 THEN PROC state(sGameOver) : ENDPROC
PROC state(sInvuln)
ENDPROC


#; 
#; *** ENTER STATE: GAME OVER  ***
#; 
DEFPROC sGameOver()
timer = TIME
LAYER %0: PRINT AT %10,%12;"Game Over"
LAYER %2,%1
IF score > high THEN high=score: DRIVER %49,%2,%6:PROC saveHS(high)
PROC hiScore()
ENDPROC
#; 
#; *** STATE: GAME OVER  ***
#; 
DEFPROC gameOver()
PROC display()
#; Wait for 2 seconds
IF (TIME-timer)< 100 THEN ENDPROC 
%i = INPUT 1| INPUT 0
IF %i&$10 THEN PROC state(sTitle)
ENDPROC


#;-----------------------------
#;
#; Utility Functions
#; Do NOT change state in these
#;
#;-----------------------------


#; 
#; *** INITIALIZE GRAPHICS ***
#; 
#; Set up Layers and Sprites
DEFPROC initGraphics()

#; Layers
LAYER 0: BORDER 0: PAPER 0: INK 7: CLS :LAYER CLEAR: LAYER OVER 6
LAYER 2,1: LAYER ERASE 0,0,256,192

#; Sprites
BANK NEW bankId
LOAD "sprites.spr" BANK bankId
SPRITE CLEAR : SPRITE BANK bankId:BANK bankId CLEAR
SPRITE PRINT 1: SPRITE BORDER 1: SPRITE STOP 

ENDPROC 

#; Set up AY sound driver and load sound effects
#; If driver is already installed, will throw an error
#; So this code will catch and ignore the error
DEFPROC initSnd()
    ON ERROR ENDPROC  
    .install "ayfx.drv"
    BANK NEW %i
    LOAD "sfx.afb" BANK %i
    DRIVER 49,1,%i
ENDPROC

#; 
#; *** HANDLE INPUT ***
#; 
DEFPROC handleInput()
%i = INPUT 1| INPUT 0
IF %i&1 THEN %x+=3
IF %i&2 THEN %x-=3
#; Prevent user holding down the fire button
IF %i&$10 THEN %a+=1 : ELSE %a=0
IF %a=1 THEN PROC fire()
IF %x<32 THEN %x=%32
IF %x>271 THEN %x=%272
ENDPROC 


#; 
#; *** UPDATE DISPLAY  ***
#; 
#;Scroll star field downwards and move sprites 
DEFPROC display()
%l-=1:IF %l=-1 THEN %l=191
#! Update player sprite position and pattern
SPRITE %0,%x,%y:SPRITE MOVE INT:LAYER AT %0,%l
ENDPROC 


#; 
#; *** COLLISION DETECTION ***
#; 
DEFPROC colDet()
FOR %i=%7 TO %9
%h = SPRITE OVER (%i,%10 TO %20,%7,%7) 
IF %h>0 THEN PROC destroyEnemy(%h,%i)
NEXT %i

#; Return true if player has been hit
ENDPROC = SPRITE OVER (%0,%20 TO %29,%7,%2)

#; 
#; *** UPDATE SCORE ***
#; 
DEFPROC score()
LAYER %0: PRINT AT %0,%0;score:LAYER %2,%1
ENDPROC 
DEFPROC hiScore()
LAYER %0: PRINT AT %0,%14;high:LAYER %2,%1
ENDPROC 



#; 
#; *** ANIMATE ENEMY SPRITES ***
#;
#; Cannot use SPRITE CONTINUE to automate the pattern change
#; as it is been updated to quickly, (set a rate will slow movement)
#; So need manually change the pattern every 10 calls
#;
DEFPROC anim() 
%b+=1: IF (%b<>10) AND (%b<>20) THEN ENDPROC
%j=%d
#;Animate mothersip and spider
SPRITE CONTINUE %10,,,%46:SPRITE CONTINUE %20,,,%38
IF %b=20 THEN %j+=1:%b=0: SPRITE CONTINUE %10,,,%47: SPRITE CONTINUE %20,,,%39
#;Animate enemies
FOR %i=%11 TO %19
SPRITE CONTINUE %i,,,%j
NEXT %i
ENDPROC

#; 
#; *** UPDATE LIVES ***
#; 
DEFPROC lives()
FOR %i=2 TO lives :SPRITE %60+i,16+(%i-2)*18,%230,%8,1:NEXT %i
FOR %i=lives+1 TO 10:SPRITE %60+i,0,0,%8,0:NEXT %i
ENDPROC

#; 
#; *** PLAYER FIRE ***
#; 
DEFPROC fire()
#; Exit if 3 missiles flying
IF SPRITE 9 THEN ENDPROC
DRIVER %49,%2,%11
SPRITE %m,%x,%200,%36,%1
SPRITE CONTINUE %m,%x,%0 TO %200 STEP -8 RUN,%36 TO %37,%3
%m+=%1: IF %m > 9 THEN %m=%7
#; Reduce bonus
bonus-=10
ENDPROC 


#; 
#; *** DESTROY ENEMY ***
#; 
DEFPROC destroyEnemy(%s,%b)
#; Get enemy x,y pos
%i=SPRITE AT (%s,0)
%j= SPRITE AT (%s,1)
PROC bang(%i,%j)
SPRITE %s,%0,%0,%0,%0: SPRITE %b,%0,%0,%0,%0
#;Spider bomb 
IF %s=20: score+=500: DRIVER %49,%2,%1
#;Mothership 
ELSE IF %s=10:score+=250: DRIVER %49,%2,%1
#;Decrease normal enemy count
ELSE:%c-=%1:score+=50
ENDIF
DRIVER %49,%2,%21
PROC score()
ENDPROC


#; 
#; *** CLEAR ENEMIES ***
#; 
DEFPROC clearEnemies()
FOR %i=%10 TO %19
SPRITE %i,%0,%0,%0,%0: SPRITE %i+10,%0,%0,%0,%0
NEXT %i
ENDPROC

DEFPROC clearBombs()
FOR %i=%20 TO %29
SPRITE %i,%0,%0,%0,%0
NEXT %i
ENDPROC

#; 
#; *** ENEMY BOMB ***
#; Temporary vars:
#; %g - sprite id
#; %h - bomb id
#; %i - sprite x
#; %j - sprite y
#; %k - probability
#; %f - spider bomb timer
#;
#; Global
#; %x - player x pos
#; %w - wave
#; 
DEFPROC bomb()
%g=%e+10:%h=%e+20:%e+=1: IF %e=10 THEN %e=%0
#;Deal with spider bomb
IF %f=1 THEN SPRITE %20,%0,%0,%0,%0
IF %f>0 THEN %f-=1
 
#; Check if there is an enemy
#; Does a bomb sprite already exist
IF % NOT SPRITE g OR SPRITE h THEN ENDPROC
%i= SPRITE AT (%g,%0)

#;Drop a spider bomb
IF %g=10 AND (w>3) AND (i>300) THEN SPRITE %h,%i,32,%39,%1:SPRITE CONTINUE %h,%32 to %255 STEP -RND(4) RUN,%(w*2+100) TO %225 STEP %(w>>4+3) RUN,%38 TO %39,%0: %f=%(6*w+160):ENDPROC
IF %g=10 THEN ENDPROC

#;More chance of dropping bomb closer to enemy
IF %x>i THEN %k=%x-i:ELSE %k=%i-x
IF %b(w) < RND (k+75) THEN ENDPROC
%j = SPRITE AT (%g,%1)
SPRITE %h,%i,%j,%5,%1
SPRITE CONTINUE %h,,%0 TO 225 STEP %b[w] RUN ,%5,%3
ENDPROC 

#;
#; Defender style explosion for enemies
#;
DEFPROC bang(%x,%y)
SPRITE 50,%x,%y,%43,%1
SPRITE 51,%x,%y,%43,%1
SPRITE 52,%x,%y,%43,%1
SPRITE 53,%x,%y,%43,%1
%j=%1+ RND 10
%k=%10-j
SPRITE CONTINUE %50,%0 TO %x  STEP %SGN{-j} RUN, %0 TO %y STEP %SGN{-k} RUN, %43,%3
%j=%1+ RND 10
%k=%10 - j
SPRITE CONTINUE %51,%x TO %256 STEP %j RUN, %y TO %192 STEP %k RUN, %43,%3
%j=%1+ RND 10
%k=%10 - j
SPRITE CONTINUE %52,%x TO %256 STEP %j RUN, %0 TO %y STEP %SGN{-k} RUN, %43,%3
%j=%1+ RND 10
%k=%10 - j
SPRITE CONTINUE %53,%0 TO %x STEP % SGN {-j} RUN ,%y TO %192 STEP %k RUN ,%43,%3
ENDPROC

DEFPROC star()
IF TIME -timer < 2 THEN ENDPROC
timer=TIME
%j=% RND (20)-10:%k=% RND (20)-10
IF %(j=0) AND (k=0) THEN ENDPROC
%h=%40+RND(5)
SPRITE %z,%150+RND(20),%118+RND(20),%h,%1
SPRITE CONTINUE %z,%0 TO %319 STEP % SGN {j} RUN ,%0 TO %255 STEP % SGN {k} RUN ,%h,%3
%z+=1:IF %z=120 THEN %z=80
ENDPROC

DEFPROC rndFormation()
FOR %i=%1 TO %v
SPRITE %10+i,%30 + RND 200,%30 + RND 120,%d,%1
SPRITE CONTINUE %10+i,%16 TO %304 STEP %1 RUN ,%18 TO %150 STEP %2 RUN,%d,%0
NEXT %i
ENDPROC

DEFPROC formation(%h)
%j=(%h-1)*72+1
FOR %i=%1 TO %v
SPRITE %10+i,%e[j],%e[j+1],%d,1
SPRITE CONTINUE %10+i,%e[j+2] TO %e[j+3] STEP %e[j+4] RUN ,%e[j+5] TO %e[j+6] STEP %e[j+7] RUN,%d,0
%j+=8
NEXT %i
ENDPROC

#;
#; High Score Load/Save
#;
DEFPROC loadHS()
LOCAL h()
ON ERROR ENDPROC =10000
LOAD "hs.bin" DATA h()
ENDPROC =h(1)

DEFPROC saveHS(hi)
LOCAL h() : DIM h(1) : h(1)= hi
ON ERROR ENDPROC
SAVE "hs.bin" DATA h()
ENDPROC

#; 
#; *** DEBUG PRINT ***
#; 
DEFPROC dbg(%i)
LAYER %0: PRINT AT %21,%0;%i;"    ":LAYER %2,%1
ENDPROC

DEFPROC font()
LOAD "Reactor.ch8" CODE %$FA00,768
POKE 23607,%$F9: POKE 23606,0:PRINT CHR$ 2
ENDPROC

#autoline