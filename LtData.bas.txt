#program LtData
#autoline 10,10

; Mark Bailey
; 17 Jan 2024
; Generate data for Nexions
;
; q%() X pos
; r%() Y Pos
; s%() xLower
; t%() xUpper
; u%() xStep
; v%() yLower
; w%() yUpper
; x%() yStep
; b%() Difficulty
; c%() Formation
; p%() Sprite Pattern
;
RUN AT 3
DIM pat(9,8)
;PROC unitTests()
RESTORE @Formation4
PROC readPattern()
PROC convertPattern(1)
PROC display()
;PROC dump(1)
STOP

DEFPROC display()
#; Layers
LAYER 0: BORDER 2: PAPER 0: INK 7: CLS 
LAYER CLEAR
;LAYER OVER 6
LAYER 2,1: LAYER ERASE 0,0,256,192

#; Sprites
BANK NEW bankId
LOAD "sprites.spr" BANK bankId
SPRITE CLEAR 
SPRITE BANK bankId
BANK bankId CLEAR
SPRITE PRINT 1
SPRITE STOP

FOR %i=%1 TO %9
    SPRITE %i,%q(i),%r(i),22,1
    SPRITE CONTINUE %i,%s(i) TO %t(i) STEP %u(i) RUN ,%v(i) TO %w(i) STEP %x(i) RUN,22,0
NEXT %i

@loop: SPRITE MOVE INT : GO TO @loop
ENDPROC

DEFPROC dump(index)
FOR %i=%1 TO %9
    PRINT %i;",";%q(i);",";%r(i);",";%s(i);"-";%t(i);":";%u(i);" ";%v(i);"-";%w(i);":";%x(i)
NEXT %i

ENDPROC

DEFPROC readData()
    DIM title$(30,18)
    RESTORE @Wave
    FOR %i=1 TO 30
        READ title$(%i),%p(i),%b(i)
    NEXT %i
ENDPROC

DEFPROC readPattern()
    LOCAL index
    FOR i=1 TO 8
        READ a$
        FOR j=1 TO 15
            IF a$(j)<>" " THEN index = VAL(a$(j)):pat(index,1)=j-1:pat(index,2)=i-1
        NEXT j
    NEXT i
    FOR i=1 TO 9
        READ xStart,xEnd,xStep,yStart,yEnd,yStep
        pat(i,3)=xStart
        pat(i,4)=xEnd
        pat(i,5)=xStep
        pat(i,6)=yStart
        pat(i,7)=yEnd
        pat(i,8)=yStep
    NEXT i
ENDPROC

;
; Convert Data in pat() array to INT arrays
; X,Y grid co-ords to pixel co-ords 
; Relative offsets to absolute pixel values
;        pat(i,1)= X Pos
;        pat(i,2)= Y Pos
;        pat(i,3)=xStart
;        pat(i,4)=xEnd
;        pat(i,5)=xStep
;        pat(i,6)=yStart
;        pat(i,7)=yEnd
;        pat(i,8)=yStep
;
;%i - Index into %INT[]
DEFPROC convertPattern(%i)
    FOR j=1 TO 9
        x=32+pat(j,1)*16
        y=48+pat(j,2)*16

        %q(i) = x :; X Pos
        %r(i) = y :; Y Pos
        %s(i) = x + pat(j,3)*16:; xLower
        %t(i) = x + pat(j,4)*16:; xUpper
        %u(i) = pat(j,5):; xStep
        %v(i) = y + pat(j,6)*16:; yLower
        %w(i) = y + pat(j,7)*16:; yUpper
        %x(i)=pat(j,8): ; yStep
        %i+=1
    NEXT j
ENDPROC

DEFPROC unitTests()
    PROC testReadPattern1()
    PROC testReadPattern2()
    PROC testConvertPattern1()
ENDPROC

DEFPROC testReadPattern1()
    PRINT "testReadPattern1"
    RESTORE @Formation
    PROC readPattern()
    IF pat(1,1)<>7 THEN PRINT "Failed: x pos ";pat(1,1)
    IF pat(1,2)<>5 THEN PRINT "Failed: y pos ";pat(1,2)

    IF pat(9,1)<>9 THEN PRINT "Failed: x pos ";pat(9,1)
    IF pat(9,2)<>1 THEN PRINT "Failed: y pos ";pat(9,2)
    PRINT "testReadPattern1 Finished"
ENDPROC

DEFPROC testReadPattern2()
    PRINT "testReadPattern2"
    RESTORE @Formation
    PROC readPattern()
    IF pat(1,3)<>-4 THEN PRINT "Failed:  ";pat(1,4)
    IF pat(2,4)<>4 THEN PRINT "Failed:  ";pat(2,5)
    IF pat(3,5)<>1 THEN PRINT "Failed:  ";pat(3,6)
    IF pat(4,6)<>-0.25 THEN PRINT "Failed:  ";pat(4,7)
    IF pat(5,7)<>0.5 THEN PRINT "Failed:  ";pat(5,8)
    IF pat(9,8)<>1 THEN PRINT "Failed:  ";pat(9,8)

    PRINT "testReadPattern2 Finished"
ENDPROC

DEFPROC testConvertPattern1()
    PRINT "testConvertPattern2"
    RESTORE @Formation
    PROC readPattern()
    PROC convertPattern(42)
    IF %q(48)<>96 THEN PRINT "Failed1:  ";%q(48)
    IF %r(48)<>64 THEN PRINT "Failed2:  ";%r(48)
    IF %s(48)<>32 THEN PRINT "Failed3:  ";%s(48)
    IF %t(48)<>160 THEN PRINT "Failed4:  ";%t(48)
    IF %u(48)<>1 THEN PRINT "Failed5:  ";%u(48)
    IF %v(48)<>60 THEN PRINT "Failed6:  ";%v(48)
    IF %w(48)<>72 THEN PRINT "Failed7:  ";%w(48)
    IF %x(48)<>1 THEN PRINT "Failed8:  ";%x(48)

    PRINT "testConvertPattern Finished"
ENDPROC


@Formation
; Formation Heart
; X  -4 TO +4 step 1px
; Y  -0.25 TO +0.5  step 1px
;     0123456789ABCDE
DATA "               ":;0 
DATA "     8   9     ":;1
DATA "    7     6    ":;2
DATA "     4   5     ":;3
DATA "      3 2      ":;4
DATA "       1       ":;5
DATA "               ":;6
DATA "               ":;7
DATA -4,4,1,-0.25,0.5,1
DATA -4,4,1,-0.25,0.5,1
DATA -4,4,1,-0.25,0.5,1
DATA -4,4,1,-0.25,0.5,1
DATA -4,4,1,-0.25,0.5,1
DATA -4,4,1,-0.25,0.5,1
DATA -4,4,1,-0.25,0.5,1
DATA -4,4,1,-0.25,0.5,1
DATA -4,4,1,-0.25,0.5,1
;Cross-eyed
@Formation2
DATA "9             8":;0 
DATA "      6 7      ":;1
DATA "5             4":;2
DATA "      2 3      ":;3
DATA "       1       ":;4
DATA "               ":;5
DATA "               ":;6
DATA "               ":;7
DATA -7,7,2,0,0,1:; 1
DATA -6,0,1,0,0,1:; 2
DATA 0,6,1,0,0,1:;  3
DATA -6,0,1,0,0,1:; 4
DATA 0,6,1,0,0,1:;  5
DATA -6,0,1,0,0,1:; 6
DATA 0,6,1,0,0,1:;  7
DATA -6,0,1,0,0,1:; 8
DATA 0,6,1,0,0,1:;  9
;Borg
@Formation3
DATA "               ":;0 
DATA "               ":;1
DATA "               ":;2
DATA "4 8 5          ":;3
DATA "               ":;4
DATA "6 1 7          ":;5
DATA "               ":;6
DATA "2 9 3          ":;7
DATA 0,11,1,-3,0,2
DATA 0,11,1,-3,0,2
DATA 0,11,1,-3,0,2
DATA 0,11,1,-3,0,2
DATA 0,11,1,-3,0,2
DATA 0,11,1,-3,0,2
DATA 0,11,1,-3,0,2
DATA 0,11,1,-3,0,2
DATA 0,11,1,-3,0,2
;Tick
@Formation4
DATA "              1":;0 
DATA "             2 ":;1
DATA "            3  ":;2
DATA "           4   ":;3
DATA "          5    ":;4
DATA "         6     ":;5
DATA "      9 7      ":;6
DATA "       8       ":;7
DATA -14,0,1,0,7,2
DATA -13,1,1,-1,6,1
DATA -12,2,1,-2,5,2
DATA -11,3,1,-3,4,1
DATA -10,4,1,-4,3,2
DATA -9,5,1,-5,2,1
DATA -8,6,1,-6,1,2
DATA -7,7,1,-7,0,1
DATA -6,8,2,-6,1,2

;-----------------------------
;
; WAVE DATA
; Name, Sprite pattern ID, difficulty, formation
;-----------------------------
@Wave
DATA "The Invasion",%12,%2
DATA "Venusian Spiders",%38,%2
DATA "Flagship",%16,%2
DATA "Coa Monster",%26,%2
DATA "Brain Wave",%22,%2
DATA "Whoovians",%14,%3
DATA "Sausage Time",%54,%3
DATA "Nemesis",%10,%3
DATA "Golden Age",%32,%3
DATA "Brain Drain",%22,%3
DATA "Full Spectrum",%56,%3
DATA "RTM",%0,%3
DATA "Little Acorns",%58,%4
DATA "Squid Game",%6,%4
DATA "Brain Freeze",%22,%4
DATA "!#$! Q",%18,%4
DATA "Scorpio",%30,%4
DATA "Spectres",%20,%4
DATA "Doh",%60,%4
DATA "Doomed",%28,%5
DATA "Space Lair",%24,%5
DATA "Squids In",%6,%5
DATA "Hydra",%30,%5
DATA "My Gal",%32,%5
DATA "Brainiac",%22,%6
DATA "Convoy Charger",%16,%6
DATA "Act Mooners",%26,%6
DATA "Lord Eugene",%24,%6
DATA "Big Ed",%60,%7
DATA "Kill Screen",%28,%7